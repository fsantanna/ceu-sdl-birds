#include "c.ceu"
#include "sdl/sdl.ceu"

#define PI 3.14

var SDL_Color bg = val SDL_Color(0x00,0x00,0x00,0xFF);

var& _SDL_Renderer ren;
watching SDL_go("Birds - 04 (dynamic)", 640,480, bg) => (ren)
do
    var& SDL_Texture img_up, img_dn;
    spawn SDL_open_texture(&ren, "res/clumsy_up.png") => (img_up);
    spawn SDL_open_texture(&ren, "res/clumsy_dn.png") => (img_dn);

    code/await Bird (var int y, var int speed) => FOREVER
    do
        var SDL_Rect rct = val SDL_Rect(20,y, 50,45);

        var float sin = 0;
        par do
            var int y = rct.y;
            var float x = rct.x;
            var int dt;
            every dt in SDL_DT do
                var float v = dt*speed;
                x = x + v/1000;
                rct.x = x as int;
                rct.y = y + speed/5*_sin(sin);
                sin = sin + PI*v/100000;
            end
        with
            every SDL_REDRAW do
                if ((((sin+PI/2)/PI) as int) % 2) == 0 then
                    _SDL_RenderCopy(&&outer.ren, &&outer.img_dn.tex, null,
                                    &&rct as _SDL_Rect&&);
                else
                    _SDL_RenderCopy(&&outer.ren, &&outer.img_up.tex, null,
                                    &&rct as _SDL_Rect&&);
                end
            end
        end
    end

    pool[] Bird birds;

    every 1s do
        spawn Bird(20+_rand()%350, 100+_rand()%100) in birds;
    end
end

escape 0;
